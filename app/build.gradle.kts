import java.io.File
import java.util.Properties
import org.gradle.api.tasks.Exec

plugins {
    id("com.android.application")
    id("org.jetbrains.kotlin.plugin.compose")
    id("org.jetbrains.kotlin.plugin.serialization")
    id("com.google.devtools.ksp")
}

apply(plugin = "com.huawei.agconnect")

ksp {
    arg("room.schemaLocation", file("schemas").path)
    arg("room.incremental", "true")
}

fun Project.resolveSigningProperty(name: String): String? {
    val fromGradle = providers.gradleProperty(name).orNull
    if (!fromGradle.isNullOrBlank()) return fromGradle
    val fromEnv = System.getenv(name)
    if (!fromEnv.isNullOrBlank()) return fromEnv
    val fromLocalProperties = rootProject.readLocalProperty(name)
    return if (fromLocalProperties.isNullOrBlank()) null else fromLocalProperties
}

fun Project.readLocalProperty(name: String): String? {
    val localPropsFile = rootProject.file("local.properties")
    if (!localPropsFile.exists()) return null
    val props = Properties()
    localPropsFile.inputStream().use { props.load(it) }
    return props.getProperty(name)?.trim()?.takeIf { it.isNotEmpty() }
}

fun parseVersionCodeFromName(versionName: String): Int {
    val trimmed = versionName.trim()
    val match = Regex("""^v(\d+)\.(\d+)\.(\d+)(?:-beta\.(\d+))?$""").matchEntire(trimmed)
        ?: error("appVersionName must follow vX.Y.Z or vX.Y.Z-beta.N, got: $trimmed")

    val major = match.groupValues[1].toInt()
    val minor = match.groupValues[2].toInt()
    val patch = match.groupValues[3].toInt()
    val betaPart = match.groupValues[4]

    require(minor in 0..99) { "Minor version must be in 0..99, got: $minor ($trimmed)" }
    require(patch in 0..99) { "Patch version must be in 0..99, got: $patch ($trimmed)" }

    val suffix = if (betaPart.isEmpty()) {
        99
    } else {
        val beta = betaPart.toInt()
        require(beta in 1..98) {
            "Beta build number must be in 1..98, got: $beta ($trimmed)"
        }
        beta
    }

    return major * 1_000_000 + minor * 10_000 + patch * 100 + suffix
}

val releaseStoreFile = project.resolveSigningProperty("PUSHGO_RELEASE_STORE_FILE")
val releaseStorePassword = project.resolveSigningProperty("PUSHGO_RELEASE_STORE_PASSWORD")
val releaseKeyAlias = project.resolveSigningProperty("PUSHGO_RELEASE_KEY_ALIAS")
val releaseKeyPassword = project.resolveSigningProperty("PUSHGO_RELEASE_KEY_PASSWORD")
val appVersionName = providers.gradleProperty("pushgo.versionName").orNull?.trim()?.takeIf { it.isNotEmpty() }
    ?: "v1.3.0"
val appVersionCode = parseVersionCodeFromName(appVersionName)
val enableAbiSplits = when (val value = providers.gradleProperty("pushgo.enableAbiSplits").orNull?.trim()?.lowercase()) {
    null -> true
    "true" -> true
    "false" -> false
    else -> error("Invalid pushgo.enableAbiSplits value: $value")
}
val rustBuildScript: File = rootProject.file("native/quinn-jni/build-android.sh")
val verifyJniContractScript: File = rootProject.file("scripts/verify_jni_contract.sh")
val generatedRustJniDir: File = layout.buildDirectory.dir("generated/rustJniLibs/main").get().asFile
val androidMinSdk = providers.gradleProperty("pushgo.androidMinSdk").get().toInt()
val androidNdkVersion = providers.gradleProperty("pushgo.androidNdkVersion").get()
val cargoNdkVersion = providers.gradleProperty("pushgo.cargoNdkVersion").get()
val privateCertPinSha256 = project.resolveSigningProperty("PUSHGO_PRIVATE_CERT_PIN_SHA256")
    ?.trim()
    ?.replace("\"", "")
    ?: ""
val updateFeedUrl = project.resolveSigningProperty("PUSHGO_UPDATE_FEED_URL")
    ?.trim()
    ?.takeIf { it.isNotEmpty() }
    ?: "" // Configure an HMS-specific signed feed; the official feed distributes FCM builds.
val updateFeedEcdsaP256PublicKeyB64 = project.resolveSigningProperty("PUSHGO_UPDATE_FEED_ECDSA_P256_PUBLIC_KEY_B64")
    ?.trim()
    ?.replace("\"", "")
    ?: ""

val buildRustJniLibs = tasks.register<Exec>("buildRustJniLibs") {
    group = "build"
    description = "Build the Rust JNI libraries used by Android packaging."
    workingDir = rustBuildScript.parentFile
    commandLine("bash", rustBuildScript.absolutePath)
    environment("PUSHGO_ANDROID_JNI_OUT_DIR", generatedRustJniDir.absolutePath)
    environment("PUSHGO_ANDROID_MIN_SDK", androidMinSdk.toString())
    environment("PUSHGO_ANDROID_NDK_VERSION", androidNdkVersion)
    environment("PUSHGO_CARGO_NDK_VERSION", cargoNdkVersion)
    inputs.file(rustBuildScript)
    listOf(
        rootProject.file("native/quinn-jni/Cargo.toml"),
        rootProject.file("native/quinn-jni/Cargo.lock"),
    ).filter(File::exists).forEach(inputs::file)
    listOf(
        rootProject.file("native/quinn-jni/src"),
        rootProject.file("native/quinn-jni/include"),
    ).filter(File::exists).forEach(inputs::dir)
    outputs.dir(generatedRustJniDir)
}

val verifyRustJniContract = tasks.register<Exec>("verifyRustJniContract") {
    group = "verification"
    description = "Verify the versioned Rust/Kotlin JNI ABI contract."
    workingDir = rootProject.projectDir
    commandLine("bash", verifyJniContractScript.absolutePath)
    inputs.file(verifyJniContractScript)
    inputs.file(rootProject.file("native/quinn-jni/jni-contract.txt"))
    inputs.file(rootProject.file("native/quinn-jni/src/lib.rs"))
    inputs.file(rootProject.file("app/src/main/java/io/ethan/pushgo/notifications/WarpLinkNativeBridge.kt"))
}

android {
    namespace = "io.ethan.pushgo"
    compileSdk { version = release(37) { minorApiLevel = 0 } }
    ndkVersion = androidNdkVersion
    project.resolveSigningProperty("PUSHGO_HMS_DEBUG_STORE_FILE")?.let { testStore ->
        signingConfigs.getByName("debug") {
            storeFile = File(testStore)
            storePassword = "android"
            keyAlias = "androiddebugkey"
            keyPassword = "android"
        }
    }

    val releaseSigningConfig = if (
        !releaseStoreFile.isNullOrBlank()
        && !releaseStorePassword.isNullOrBlank()
        && !releaseKeyAlias.isNullOrBlank()
        && !releaseKeyPassword.isNullOrBlank()
    ) {
        signingConfigs.create("release") {
            storeFile = File(releaseStoreFile)
            storePassword = releaseStorePassword
            keyAlias = releaseKeyAlias
            keyPassword = releaseKeyPassword
        }
    } else {
        null
    }

    defaultConfig {
        applicationId = "io.ethan.pushgo"
        minSdk = androidMinSdk
        targetSdk = 36
        versionCode = appVersionCode
        versionName = appVersionName
        testInstrumentationRunner = "io.ethan.pushgo.test.PushGoAndroidJUnitRunner"
        buildConfigField("String", "PRIVATE_CERT_PIN_SHA256", "\"$privateCertPinSha256\"")
        buildConfigField("String", "DEFAULT_UPDATE_FEED_URL", "\"$updateFeedUrl\"")
        buildConfigField("String", "UPDATE_FEED_ECDSA_P256_PUBLIC_KEY_B64", "\"$updateFeedEcdsaP256PublicKeyB64\"")

        vectorDrawables {
            useSupportLibrary = true
        }
    }

    buildTypes {
        debug {
            buildConfigField("String", "DEFAULT_SERVER_ADDRESS", "\"https://gateway.pushgo.cn\"")
        }
        release {
            isMinifyEnabled = true
            isShrinkResources = true
            proguardFiles(getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro")
            buildConfigField("String", "DEFAULT_SERVER_ADDRESS", "\"https://gateway.pushgo.cn\"")
            if (releaseSigningConfig != null) {
                signingConfig = releaseSigningConfig
            }
        }
        create("hmsLan") {
            initWith(getByName("release"))
            signingConfig = signingConfigs.getByName("debug")
            matchingFallbacks += listOf("release")
            buildConfigField("String", "DEFAULT_SERVER_ADDRESS", "\"http://192.168.1.6:6666\"")
        }
    }

    splits {
        abi {
            isEnable = enableAbiSplits
            reset()
            include("armeabi-v7a", "arm64-v8a", "x86_64")
            isUniversalApk = enableAbiSplits
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    buildFeatures {
        compose = true
        buildConfig = true
    }

    packaging {
        resources {
            excludes += "/META-INF/{AL2.0,LGPL2.1}"
        }
    }

    sourceSets {
        getByName("hmsLan") {
            kotlin.directories.add("src/release/java")
        }
        getByName("main") {
            // No private-stream native libraries in the HMS distribution.
        }
    }
}

androidComponents {
    beforeVariants(selector().withBuildType("hmsLan")) {
        it.hostTests[com.android.build.api.variant.HostTestBuilder.UNIT_TEST_TYPE]?.enable = true
    }
}

kotlin {
    compilerOptions {
        jvmTarget.set(org.jetbrains.kotlin.gradle.dsl.JvmTarget.JVM_21)
    }
}

configurations.configureEach {
    if (name == "kotlinAbiValidationCompatClasspath") {
        // Keep KGP's open compatibility range on the declared Kotlin toolchain instead of a newer prerelease.
        resolutionStrategy.force("org.jetbrains.kotlin:kotlin-build-tools-impl:2.4.0")
    }
}

tasks.named("preBuild").configure {
    // The HMS distribution does not package or run the private-stream JNI.
}

tasks.named("check").configure {
    dependsOn(verifyRustJniContract)
}

tasks.register("printReleaseVersionInfo") {
    group = "help"
    description = "Prints the Android release version metadata for CI."
    doLast {
        println("versionName=$appVersionName")
        println("versionCode=$appVersionCode")
        println("applicationId=io.ethan.pushgo")
        println("abiSplitsEnabled=$enableAbiSplits")
        println("releaseAbis=armeabi-v7a,arm64-v8a,x86_64,universal")
    }
}

// APK-only distribution policy: disable release AAB tasks to avoid accidental bundle publishing.
tasks.configureEach {
    if (name in setOf(
            "buildReleasePreBundle",
            "bundleRelease",
            "packageReleaseBundle",
            "signReleaseBundle",
            "produceReleaseBundleIdeListingFile",
            "createReleaseBundleListingFileRedirect",
        )
    ) {
        enabled = false
    }
}

dependencies {
    implementation("com.huawei.hms:push:6.13.0.301")
    implementation(platform("androidx.compose:compose-bom:2026.06.01"))
    implementation("androidx.compose.ui:ui")
    implementation("androidx.compose.ui:ui-tooling-preview")
    implementation("androidx.compose.material3:material3")
    implementation("androidx.compose.material:material-icons-extended")

    implementation("androidx.core:core-ktx:1.19.0")
    implementation("androidx.activity:activity-compose:1.13.0")
    implementation("androidx.lifecycle:lifecycle-runtime-ktx:2.11.0")
    implementation("androidx.lifecycle:lifecycle-runtime-compose:2.11.0")
    implementation("androidx.lifecycle:lifecycle-viewmodel-ktx:2.11.0")
    implementation("androidx.lifecycle:lifecycle-viewmodel-compose:2.11.0")
    implementation("androidx.navigation:navigation-compose:2.9.8")
    implementation("androidx.appcompat:appcompat:1.7.1")
    implementation("com.google.android.material:material:1.14.0")

    implementation("androidx.datastore:datastore-preferences:1.2.1")
    implementation("androidx.security:security-crypto:1.1.0")

    implementation("androidx.room:room-runtime:2.8.4")
    implementation("androidx.room:room-ktx:2.8.4")
    implementation("androidx.room:room-paging:2.8.4")
    ksp("androidx.room:room-compiler:2.8.4")

    implementation("androidx.paging:paging-runtime-ktx:3.5.0")
    implementation("androidx.paging:paging-compose:3.5.0")

    implementation("org.jetbrains.kotlinx:kotlinx-serialization-json:1.11.0")
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-android:1.11.0")
    implementation("androidx.work:work-runtime-ktx:2.11.2")
    implementation("io.coil-kt.coil3:coil-compose:3.5.0")
    implementation("io.coil-kt.coil3:coil-gif:3.5.0")
    implementation("io.noties.markwon:core:4.6.2")
    implementation("io.noties.markwon:ext-strikethrough:4.6.2")
    implementation("io.noties.markwon:ext-tables:4.6.2")
    implementation("io.noties.markwon:ext-tasklist:4.6.2")
    implementation("io.noties.markwon:html:4.6.2")
    implementation("io.noties.markwon:linkify:4.6.2")
    implementation("io.noties.markwon:image:4.6.2")

    debugImplementation("androidx.compose.ui:ui-tooling")
    testImplementation("junit:junit:4.13.2")
    testImplementation("org.json:json:20260719")
    androidTestImplementation(platform("androidx.compose:compose-bom:2026.06.01"))
    androidTestImplementation("androidx.test.ext:junit:1.3.0")
    androidTestImplementation("androidx.test:runner:1.7.0")
    androidTestImplementation("androidx.test.uiautomator:uiautomator:2.4.0")
    androidTestImplementation("androidx.test.espresso:espresso-core:3.7.0")
    androidTestImplementation("androidx.compose.ui:ui-test-junit4")
    debugImplementation("androidx.compose.ui:ui-test-manifest")
    kspAndroidTest("androidx.room:room-compiler:2.8.4")

}
